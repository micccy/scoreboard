				;16 clock cycles used before / 4 clock cycles used after - 36 remaining

                sbiw    SEC_L,1                     ;  2      	Decrease the main-loops-in-a-second word counter
                brne    op0_norel                	;  2 1    	If not zero jump over and do something else/waste time

                ldi     SEC_H,HIGH(SECLOOP)         ;    1    	Otherwise reload the counter
                ldi     SEC_L,LOW(SECLOOP)          ;    1
                ldi     TMP,3                       ;    1
                and     TMP,PERIOD                  ;    1    	If this is the end of second #0 or #4 in a 7seconds period
													;			increase the counter by one to achieve a 4000000/(56*16) avg
                brne    PC+2                        ;    1
                inc     SEC_L                       ;    1

                inc     PERIOD                      ;    1    	Increase the current second # in a 7seconds period counter
                cpi     PERIOD,7                    ;    1		If we reach 7 reset it to zero
                brlo    PC+2                        ;    1
                clr     PERIOD                      ;    1

op0_opsec:      ldi     TMP,HLTC                    ;    1  	Toggle latch output rest position
                eor     HFLAGS,TMP                  ;    1

                inc     SECOND                      ;    1  	Increase current second value
                ldi     TMP,60                      ;    1  	Load temp with the number of seconds in each minute
                ldi     HTMP,FLPSEC                 ;    1
                and     HTMP,FLAGS                  ;    1
                lsr     HTMP                        ;    1
                lsr     HTMP                        ;    1
                add     TMP,HTMP                    ;    1  	Add one if this minute has a leap second
                cp      SECOND,TMP                  ;    1
                brlo    op0_nomin                  	;    1 2 	Check if a minute has passed, if not jump over
                ori     FLAGS,FUPMIN                ;    1  	Set the minute passed flag
                clr     SECOND                      ;    1  	Azzero i secondi

                rjmp    op0_syncclr					;    2		Waste some cycles and proceed

op0_norel:   	WASTE   30							; 30
				rjmp	op0_done					;  2

op0_nomin:     	nop                                	;      1
               	nop                					;      1
               	nop                					;      1

op0_syncclr:	WASTE	 8
op0_done:
                ;30 clock cycles partially available
